import Mathlib.Data.Set.Function
import MyProject.Green.Basic

/-!
# Green's Lemma

This file proves Green's lemma, which is the following:
Let `x ğ“¡ y` such that `x = y * u` and `y = x * v`.
Then the map `x â†¦ x * v` is a bijection from the ğ“›-class of `x` to the ğ“›-class of `y`,
and the map `x â†’ x * u` is its inverse. Additionally, these bijections preserve ğ“— classes.

We also prove the dual version of this lemma.

## Main Theorems

Let `x ğ“¡ y` such that `x = y * u` and `y = x * v`.

* `REquiv.inv_on_lClass` - the map `x â†¦ x * u` is the inverse of `x â†¦ x * v` on the ğ“›-class of `x`.
* `REquiv.bij_on_lClass` - the map `x â†¦ x * v` is a bijection from the
ğ“›-class of `x` to the ğ“›-class of `y`.
* `REquiv.bij_on_lClass_pres_hClass` - this bijection preserves ğ“— classes.

Let `x ğ“› y` such that `x = u * y` and `y = v * x`.

* `LEquiv.inv_on_rClass` - the map `x â†¦ v * x` is the inverse of `x â†¦ u * x` on the ğ“¡-class of `x`.
* `LEquiv.bij_on_rClass` - the map `x â†¦ u * x` is a bijection from the
ğ“¡-class of `x` to the ğ“¡-class of `y`.
* `LEquiv.bij_on_rClass_pres_hClass` - this bijection preserves ğ“— classes.

## References

TODO

## Blueprint

* One lemma entry for the ğ“¡-class bijection and its properties.
label : greens-lemma
dependencies : todo
-/

namespace Semigroup

variable {S : Type*} [Semigroup S] {x y u v w : S}

/-- If `x ğ“¡ y` such that `x = y * u` and `y = x * v`, then right translation by `v * u` on any
element ğ“›-equivalent to `x` is the idenity. -/
lemma REquiv.translation_id (hx : x = y * u) (hy : y = x * v) (hw : x ğ“› w) : w * v * u = w := by
  rcases hw with âŸ¨_, âŸ¨z, hzâŸ©âŸ©
  cases z with
  | one => simp at hz; subst hz; rw [â† hy, hx]
  | coe z =>
    simp [â† WithOne.coe_mul] at hz
    subst hz
    rw [mul_assoc z, â† hy, mul_assoc, â† hx]

/-- If `y â‰¤ğ“¡ x` such that `y = x * v`, then the map `w â†¦ w * v`
maps the ğ“›-class of `x` to that of `y` -/
lemma REquiv.map_on_lClass (hy : y = x * v) :
    Set.MapsTo (fun w â†¦ w * v) âŸ¦xâŸ§ğ“› âŸ¦yâŸ§ğ“› := by
  simp [Set.MapsTo]
  intros z hz
  rw [hy]
  apply LEquiv.rmult_compat
  exact hz

/-- If `x ğ“¡ y` such that `x = y * u` and `y = x * v`, then the map `w â†¦ w * u` is injective on the
ğ“›-class of `x`. -/
lemma REquiv.inj_on_lClass (hx : x = y * u) (hy : y = x * v) :
    Set.InjOn (fun w â†¦ w * v) âŸ¦xâŸ§ğ“› := by
  simp [Set.InjOn]
  intros w hw z hz heq
  have hwâ‚‚ := REquiv.translation_id hx hy hw.symm
  have hzâ‚‚ := REquiv.translation_id hx hy hz.symm
  rw [â† hwâ‚‚, â† hzâ‚‚, heq]

/-- If `x ğ“¡ y` such that `x = y * u` and `y = x * v`, then the map `w â†¦ w * u` is surjective
from the ğ“›-class of `x` to that of `y`. -/
lemma REquiv.surj_on_lClass (hx : x = y * u) (hy : y = x * v) :
    Set.SurjOn (fun w â†¦ w * v) âŸ¦xâŸ§ğ“› âŸ¦yâŸ§ğ“› := by
  simp [Set.SurjOn]
  intros z hz
  simp at hz âŠ¢
  use z * u
  constructor
  Â· rw [hx]
    apply LEquiv.rmult_compat
    exact hz
  Â· have hl : y ğ“› y := by simp
    apply REquiv.translation_id hy hx hz.symm

/-- If `x ğ“¡ y` such that `x = y * u` and `y = x * v`, then the map `w â†¦ w * u` is the inverse of
`w â†¦ w * v` when restricted to the ğ“›-classes of `x` and `y` -/
theorem REquiv.inv_on_lClass (hx : x = y * u) (hy : y = x * v) :
    Set.InvOn (fun w â†¦ w * u) (fun w â†¦ w * v) âŸ¦xâŸ§ğ“› âŸ¦yâŸ§ğ“› := by
  simp [Set.InvOn, Set.LeftInvOn]
  constructor
  Â· intro z hz
    apply REquiv.translation_id hx hy hz.symm
  Â· intro z hz
    apply REquiv.translation_id hy hx hz.symm

/-- If `x ğ“¡ y` such that `x = y * u` and `y = x * v`, then the map `w â†¦ w * v` is a bijection from
the ğ“›-class of `x` to that of `y`. -/
theorem REquiv.bij_on_lClass (hx : x = y * u) (hy : y = x * v) :
    Set.BijOn (fun w â†¦ w * v) âŸ¦xâŸ§ğ“› âŸ¦yâŸ§ğ“› := by
  refine Set.BijOn.mk ?_ ?_ ?_
  Â· apply REquiv.map_on_lClass hy
  Â· apply REquiv.inj_on_lClass hx hy
  Â· apply REquiv.surj_on_lClass hx hy

/-- If `x ğ“¡ y` such that `x = y * u` and `y = x * v`,
then the map `w â†¦ w * v` preserves ğ“—-classes. -/
theorem REquiv.bij_on_lClass_pres_hClass (hx : x = y * u) (hy : y = x * v)
  (hw : x ğ“› w) (hz : x ğ“› z) : w ğ“— z â†” w * v ğ“— z * v := by
  constructor
  Â· intro h
    rw [HEquiv.iff_rEquiv_and_lEquiv] at h âŠ¢
    constructor
    Â· rcases h with âŸ¨âŸ¨âŸ¨a, haâŸ©, âŸ¨b, hbâŸ©âŸ©, _âŸ©
      constructor
      Â· use u * a * v
        have hzâ‚‚ : â†‘z * â†‘v * â†‘u = (â†‘z : WithOne S) := by
          simp [â† WithOne.coe_mul]
          exact REquiv.translation_id hx hy hz
        simp [â† mul_assoc]
        rw [hzâ‚‚, â† ha]
      Â· use u * b * v
        have hwâ‚‚ : â†‘w * â†‘v * â†‘u = (â†‘w : WithOne S) := by
          simp [â† WithOne.coe_mul]
          exact REquiv.translation_id hx hy hw
        simp [â† mul_assoc]
        rw [hwâ‚‚, â† hb]
    Â· apply LEquiv.rmult_compat w z v h.2
  Â· intros h
    rw [HEquiv.iff_rEquiv_and_lEquiv]
    constructor
    Â· have hrâ‚ : w ğ“¡  w * v := by
        simp [REquiv]
        use u
        have hwâ‚‚ := REquiv.translation_id hx hy hw
        simp [â† WithOne.coe_mul]
        symm
        apply hwâ‚‚
      rw [HEquiv.iff_rEquiv_and_lEquiv] at h
      have hrâ‚‚ : w * v ğ“¡ z * v := h.1
      have hrâ‚ƒ : w ğ“¡ z * v := REquiv.trans hrâ‚ hrâ‚‚
      have hrâ‚„ : z * v ğ“¡ z := by
        simp [REquiv]
        use u
        have hzâ‚‚ := REquiv.translation_id hx hy hz
        simp [â† WithOne.coe_mul]
        symm
        apply hzâ‚‚
      apply REquiv.trans hrâ‚ƒ hrâ‚„
    Â· apply LEquiv.trans hw.symm hz

/-! ### Dual proofs -/

/-- If `x ğ“› y` such that `x = u * y` and `y = v * x`, then left translation by `u * v` on any
element ğ“¡-equivalent to `x` is the identity. -/
lemma LEquiv.translation_id (hx : x = u * y) (hy : y = v * x) (hw : x ğ“¡ w) : u * v * w = w := by
  rcases hw with âŸ¨_, âŸ¨z, hzâŸ©âŸ©
  cases z with
  | one =>
    simp at hz
    subst hz
    rw [mul_assoc, â† hy, hx]
  | coe z =>
    simp [â† WithOne.coe_mul] at hz
    subst hz
    rw [â† mul_assoc, mul_assoc u, â† hy, â† hx]

/-- If `y â‰¤ğ“› x` such that `y = v * x`, then the map `w â†¦ v * w`
maps the ğ“¡-class of `x` to that of `y` -/
lemma LEquiv.map_on_rClass (hy : y = v * x) :
    Set.MapsTo (fun w â†¦ v * w) âŸ¦xâŸ§ğ“¡ âŸ¦yâŸ§ğ“¡ := by
  simp [Set.MapsTo]
  intros z hz
  rw [hy]
  apply REquiv.lmult_compat
  exact hz

/-- If `x ğ“› y` such that `x = u * y` and `y = v * x`, then the map `w â†¦ v * w` is injective on the
ğ“¡-class of `x`. -/
lemma LEquiv.inj_on_rClass (hx : x = u * y) (hy : y = v * x) :
    Set.InjOn (fun w â†¦ v * w) âŸ¦xâŸ§ğ“¡ := by
  simp [Set.InjOn]
  intros w hw z hz heq
  have hwâ‚‚ := LEquiv.translation_id hx hy hw.symm
  have hzâ‚‚ := LEquiv.translation_id hx hy hz.symm
  rw [â† hwâ‚‚, â† hzâ‚‚]
  simp [mul_assoc, heq]

/-- If `x ğ“› y` such that `x = u * y` and `y = v * x`, then the map `w â†¦ v * w` is surjective
from the ğ“¡-class of `x` to that of `y`. -/
lemma LEquiv.surj_on_rClass (hx : x = u * y) (hy : y = v * x) :
    Set.SurjOn (fun w â†¦ v * w) âŸ¦xâŸ§ğ“¡ âŸ¦yâŸ§ğ“¡ := by
  simp [Set.SurjOn]
  intros z hz
  simp at hz âŠ¢
  use u * z
  constructor
  Â· rw [hx]
    apply REquiv.lmult_compat
    exact hz
  Â· have hr : y ğ“¡ y := by simp
    rw [â† mul_assoc]
    apply LEquiv.translation_id hy hx hz.symm

/-- If `x ğ“› y` such that `x = u * y` and `y = v * x`, then the map `w â†¦ u * w` is the inverse of
`w â†¦ v * w` when restricted to the ğ“¡-classes of `x` and `y` -/
theorem LEquiv.inv_on_rClass (hx : x = u * y) (hy : y = v * x) :
    Set.InvOn (fun w â†¦ u * w) (fun w â†¦ v * w) âŸ¦xâŸ§ğ“¡ âŸ¦yâŸ§ğ“¡ := by
  simp [Set.InvOn, Set.LeftInvOn]
  constructor
  Â· intro z hz
    rw [â† mul_assoc]
    apply LEquiv.translation_id hx hy hz.symm
  Â· intro z hz
    rw [â† mul_assoc]
    apply LEquiv.translation_id hy hx hz.symm

/-- If `x ğ“› y` such that `x = u * y` and `y = v * x`, then the map `w â†¦ v * w` is a bijection from
the ğ“¡-class of `x` to that of `y`. -/
theorem LEquiv.bij_on_rClass (hx : x = u * y) (hy : y = v * x) :
    Set.BijOn (fun w â†¦ v * w) âŸ¦xâŸ§ğ“¡ âŸ¦yâŸ§ğ“¡ := by
  refine Set.BijOn.mk ?_ ?_ ?_
  Â· apply LEquiv.map_on_rClass hy
  Â· apply LEquiv.inj_on_rClass hx hy
  Â· apply LEquiv.surj_on_rClass hx hy

/-- If `x ğ“› y` such that `x = u * y` and `y = v * x`,
then the map `w â†¦ v * w` preserves ğ“—-classes. -/
theorem LEquiv.bij_on_rClass_pres_hClass (hx : x = u * y) (hy : y = v * x)
  (hw : x ğ“¡ w) (hz : x ğ“¡ z) : w ğ“— z â†” v * w ğ“— v * z := by
  constructor
  Â· intro h
    rw [HEquiv.iff_rEquiv_and_lEquiv] at h âŠ¢
    constructor
    Â· apply REquiv.lmult_compat w z v h.1
    Â· rcases h with âŸ¨_, âŸ¨âŸ¨a, haâŸ©, âŸ¨b, hbâŸ©âŸ©âŸ©
      constructor
      Â· use v * a * u
        have hzâ‚‚ : â†‘u * â†‘v * â†‘z = (â†‘z : WithOne S) := by
          simp [â† WithOne.coe_mul]
          exact LEquiv.translation_id hx hy hz
        simp
        rw [mul_assoc]
        conv => rhs; rhs; rw [â† mul_assoc, hzâ‚‚]
        rw [ha, mul_assoc]
      Â· use v * b * u
        have hwâ‚‚ : â†‘u * â†‘v * â†‘w = (â†‘w : WithOne S) := by
          simp [â† WithOne.coe_mul]
          exact LEquiv.translation_id hx hy hw
        simp
        rw [mul_assoc]
        conv => rhs; rhs; rw [â† mul_assoc, hwâ‚‚]
        rw [hb, mul_assoc]
  Â· intros h
    rw [HEquiv.iff_rEquiv_and_lEquiv]
    constructor
    Â· apply REquiv.trans hw.symm hz
    Â· have hlâ‚ : w ğ“› v * w := by
        simp [LEquiv]
        use u
        have hwâ‚‚ := LEquiv.translation_id hx hy hw
        simp [â† WithOne.coe_mul]
        symm
        rw [â† mul_assoc]
        apply hwâ‚‚
      rw [HEquiv.iff_rEquiv_and_lEquiv] at h
      have hlâ‚‚ : v * w ğ“› v * z := h.2
      have hlâ‚ƒ : w ğ“› v * z := LEquiv.trans hlâ‚ hlâ‚‚
      have hlâ‚„ : v * z ğ“› z := by
        simp [LEquiv]
        use u
        have hzâ‚‚ := LEquiv.translation_id hx hy hz
        simp [â† WithOne.coe_mul]
        symm
        rw [â† mul_assoc]
        apply hzâ‚‚
      apply LEquiv.trans hlâ‚ƒ hlâ‚„

end Semigroup
