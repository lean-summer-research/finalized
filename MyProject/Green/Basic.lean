import Mathlib.Algebra.Group.Idempotent
import MyProject.Green.Defs

/-!
# Basic Properties of Green's Relations

This file proves basic properties about Green's relations and idempotent elements.

We also prove that Green's relations are preserved under morphisms.

## Main theorems

Characterizations of elements that are ğ“¡-below, ğ“›-below, or ğ“—-below an idempotent:
* `Semigroup.RPreorder.le_idempotent` - `x â‰¤ğ“¡ e â†” x = e * x`.
* `Semigroup.LPreorder.le_idempotent` - `x â‰¤ğ“› e â†” x = x * e`.
* `Semigroup.HPreorder.le_idempotent` - `x â‰¤ğ“— e â†” x = e * x âˆ§ x = x * e`.

Green's relations are preserved under semigroup morphisms `f`:
* `Semigroup.RPreorder.hom_pres` - `x â‰¤ğ“¡ y â†’ f x â‰¤ğ“¡ f y`.
* `Semigroup.LPreorder.hom_pres` - `x â‰¤ğ“› y â†’ f x â‰¤ğ“› f y`.
* `Semigroup.JPreorder.hom_pres` - `x â‰¤ğ“™ y â†’ f x â‰¤ğ“™ f y`.
* `Semigroup.HPreorder.hom_pres` - `x â‰¤ğ“— y â†’ f x â‰¤ğ“— f y`.
* `Semigroup.REquiv.hom_pres` - `x ğ“¡ y â†’ f x ğ“¡ f y`.
* `Semigroup.LEquiv.hom_pres` - `x ğ“› y â†’ f x ğ“› f y`.
* `Semigroup.JEquiv.hom_pres` - `x ğ“™ y â†’ f x ğ“™ f y`.
* `Semigroup.HEquiv.hom_pres` - `x ğ“— y â†’ f x ğ“— f y`.
* `Semigroup.DEquiv.hom_pres` - `x ğ““ y â†’ f x ğ““ f y`.

## References

TODO

## Blueprint

* One entry for the characterization lemmas of elements below idempotents.
Label : le-idempotent
Tagged Lean lemmas :
 - `Semigroup.RPreorder.le_idempotent`
 - `Semigroup.LPreorder.le_idempotent`
 - `Semigroup.HPreorder.le_idempotent`
Content : prove it for R and H, say L holds by a similar argument to R
Dependencies : greens-relations

* One entry for the morphism preservation lemmas.
Label : greens-relations-hom-pres
Tagged Lean lemmas :
 - `Semigroup.RPreorder.hom_pres`
 - `Semigroup.LPreorder.hom_pres`
 - `Semigroup.JPreorder.hom_pres`
 - `Semigroup.HPreorder.hom_pres`
 - `Semigroup.REquiv.hom_pres`
 - `Semigroup.LEquiv.hom_pres`
 - `Semigroup.JEquiv.hom_pres`
 - `Semigroup.HEquiv.hom_pres`
 - `Semigroup.DEquiv.hom_pres`
Content : prove â‰¤R and ğ“¡, say â‰¤L and ğ“› holds by a similar argument.
Then, prove â‰¤ğ“™ and ğ“™, then â‰¤ğ“— and ğ“—, then ğ““.
Dependencies : greens-relations
-/

/-! ### Idempotent properties (Prop 1.4.1) -/

namespace Semigroup

variable {S : Type*} [Semigroup S]

/-- An element `x` is ğ“¡-below an idempotent `e` if and only if `x = e * x`. -/
theorem RPreorder.le_idempotent (x e : S) (h: IsIdempotentElem e) :
    (x â‰¤ğ“¡ e) â†” (x = e * x) := by
  constructor
  Â· rintro âŸ¨u, hruâŸ©
    unfold IsIdempotentElem at h
    rw [â† WithOne.coe_inj, WithOne.coe_mul] at h âŠ¢
    rw [hru, â† mul_assoc, h]
  Â· intro hl; use x
    rw [â† WithOne.coe_inj] at hl; exact hl

/-- An element `x` is ğ“›-below an idempotent `e` if and only if `x = x * e`. -/
theorem LPreorder.le_idempotent (x e : S) (h: IsIdempotentElem e) :
    (x â‰¤ğ“› e) â†” (x = x * e) := by
  constructor
  Â· rintro âŸ¨u, hruâŸ©
    unfold IsIdempotentElem at h
    rw [â† WithOne.coe_inj, WithOne.coe_mul] at h âŠ¢
    rw [hru, mul_assoc, h]
  Â· intro hl; use x
    rw [â† WithOne.coe_inj] at hl; exact hl

/-- An element is ğ“—-below an idempotent if and only if it is fixed by both left and right
multiplication. -/
theorem HPreorder.le_idempotent (x e : S) (h : IsIdempotentElem e) :
    (x â‰¤ğ“— e) â†” (x = e * x âˆ§ x = x * e) := by
  simp [HPreorder, RPreorder.le_idempotent x e h, LPreorder.le_idempotent x e h]

/-- An element is ğ“—-below an idempotent if and only if it is a sandwich fixed point. -/
theorem HPreorder.le_idempotent' (x e : S) (he : IsIdempotentElem e) :
    x â‰¤ğ“— e â†” e * x * e = x := by
  rw [HPreorder.le_idempotent x e he]
  constructor
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©; rw [â† hâ‚, â† hâ‚‚]
  Â· intro h; constructor
    Â· nth_rw 2 [â† h]
      simp_rw [â† mul_assoc]; rw [he, h]
    Â· nth_rw 2 [â† h]
      rw [mul_assoc, he, h]

/-!
### Morphisms

We prove that all of Green's preorders and equivalences are preserved under morphisms.
Note that these should quantify over `MulHomClass`.
-/

variable {S T : Type*} [Semigroup S] [Semigroup T]
variable {F : Type*} [FunLike F S T] [MulHomClass F S T]

/-- The ğ“¡-preorder is preserved by semigroup morphisms. -/
theorem RPreorder.hom_pres (f : F) (x y : S) (h : x â‰¤ğ“¡ y) : f x â‰¤ğ“¡ f y := by
  obtain âŸ¨z, hzâŸ© := h
  cases z with
  | one => simp_all
  | coe z =>
    have heq : x = y * z := by
      rw [â† WithOne.coe_inj, WithOne.coe_mul]
      exact hz
    rw [â† WithOne.coe_mul, WithOne.coe_inj] at hz
    subst x
    simp

/-- The ğ“›-preorder is preserved by semigroup morphisms. -/
theorem LPreorder.hom_pres (f : F) (x y : S) (h : x â‰¤ğ“› y) : f x â‰¤ğ“› f y := by
  obtain âŸ¨z, hzâŸ© := h
  cases z with
  | one => simp_all
  | coe z =>
    have heq : x = z * y := by
      rw [â† WithOne.coe_inj, WithOne.coe_mul]
      exact hz
    rw [â† WithOne.coe_mul, WithOne.coe_inj] at hz
    subst x
    simp

/-- The ğ“™-preorder is preserved by semigroup morphisms. -/
theorem JPreorder.hom_pres (f : F) (x y : S) (h : x â‰¤ğ“™ y) : f x â‰¤ğ“™ f y := by
  obtain âŸ¨u, v, huvâŸ© := h
  cases u with
  | one =>
    cases v with
    | one => simp_all
    | coe v =>
      have heq : x = y * v := by
        rw [â† WithOne.coe_inj, WithOne.coe_mul]
        exact huv
      subst x
      simp
  | coe u =>
    cases v with
    | one =>
      have heq : x = u * y := by
        rw [â† WithOne.coe_inj, WithOne.coe_mul]
        exact huv
      subst x
      simp
    | coe v =>
      have heq : x = u * y * v := by
        rw [â† WithOne.coe_inj, WithOne.coe_mul]
        exact huv
      subst x
      simp

/-- The ğ“—-preorder is preserved by semigroup morphisms. -/
theorem HPreorder.hom_pres (f : F) (x y : S) (h : x â‰¤ğ“— y) : f x â‰¤ğ“— f y := by
  rw [HPreorder] at h âŠ¢
  exact âŸ¨RPreorder.hom_pres f x y h.1, LPreorder.hom_pres f x y h.2âŸ©

/-- The ğ“¡ equivalence is preserved by semigroup morphisms. -/
theorem REquiv.hom_pres (f : F) (x y : S) (h : x ğ“¡ y) : f x ğ“¡ f y := by
  rw [REquiv] at h âŠ¢
  exact âŸ¨RPreorder.hom_pres f x y h.1, RPreorder.hom_pres f y x h.2âŸ©

/-- The ğ“› equivalence is preserved by semigroup morphisms. -/
theorem LEquiv.hom_pres (f : F) (x y : S) (h : x ğ“› y) : f x ğ“› f y := by
  rw [LEquiv] at h âŠ¢
  exact âŸ¨LPreorder.hom_pres f x y h.1, LPreorder.hom_pres f y x h.2âŸ©

/-- The ğ“™ equivalence is preserved by semigroup morphisms. -/
theorem JEquiv.hom_pres (f : F) (x y : S) (h : x ğ“™ y) : f x ğ“™ f y := by
  rw [JEquiv] at h âŠ¢
  exact âŸ¨JPreorder.hom_pres f x y h.1, JPreorder.hom_pres f y x h.2âŸ©

/-- The ğ“— equivalence is preserved by semigroup morphisms. -/
theorem HEquiv.hom_pres (f : F) (x y : S) (h : x ğ“— y) : f x ğ“— f y := by
  rw [HEquiv] at h âŠ¢
  exact âŸ¨HPreorder.hom_pres f x y h.1, HPreorder.hom_pres f y x h.2âŸ©

/-- The ğ““ equivalence is preserved by semigroup morphisms. -/
theorem DEquiv.hom_pres (f : F) (x y : S) (h : x ğ““ y) : f x ğ““ f y := by
  rw [DEquiv] at h âŠ¢
  obtain âŸ¨z, hxz, hyzâŸ© := h
  use f z
  exact âŸ¨REquiv.hom_pres f x z hxz, LEquiv.hom_pres f z y hyzâŸ©

end Semigroup
