import Mathlib.Algebra.Group.WithOne.Defs

/-!
# Green's Relations Definitions

This file defines Green's preorders and equivalence relations on semigroups.

We prove basic theorems, like `Semigroup.rEquiv_lEquiv_comm`, which are necessary
for instantiating `DEquiv` as an equivalence relation with `Semigroup.DEquiv.isEquivalence`.

We also prove simp lemmas at the end of the file.

## Main definitions

In the following definitions, `S` is a semigroup and `x y : S`.

**Green's Preorder Relations**

* `Semigroup.RPreorder` - `x` is ğ“¡-below `y` (notated `x â‰¤ğ“¡ y`) iff there exists a
`z : WithOne S` such that `â†‘x = â†‘y * z`

* `Semigroup.LPreorder` - `x` is ğ“›-below `y` (notated `x â‰¤ğ“› y`) iff there exists a
`z : WithOne S` such that `â†‘x = z * â†‘y`

* `Semigroup.JPreorder` - `x` is ğ“™-below `y` (notated `x â‰¤ğ“™ y`) iff there exists
`z w : WithOne S` such that `â†‘x = z * â†‘y * w`

* `Semigroup.HPreorder` - `x` is ğ“—-below `y` (notated `x â‰¤ğ“— y`) iff `x â‰¤ğ“› y` and `x â‰¤ğ“¡ y`

We instantiate each preorder in the unbundled `IsPreorder` Class:
* `Semigroup.RPreorder.isPreorder` - instance for `[IsPreorder S RPreorder]`
* `Semigroup.LPreorder.isPreorder` - instance for `[IsPreorder S LPreorder]`
* `Semigroup.JPreorder.isPreorder` - instance for `[IsPreorder S JPreorder]`
* `Semigroup.HPreorder.isPreorder` - instance for `[IsPreorder S HPreorder]`

**Green's Equivalence Relations**

We define the `ğ“¡, ğ“›, ğ“™, ğ“—` relations as the
symmetric closure of their corresponding preorders:
* `Semigroup.REquiv` - `x ğ“¡ y` iff `x â‰¤ğ“¡ y` and `y â‰¤ğ“¡ x`
* `Semigroup.LEquiv` - `x ğ“› y` iff `x â‰¤ğ“› y` and `y â‰¤ğ“› x`
* `Semigroup.JEquiv` - `x ğ“™ y` iff `x â‰¤ğ“™ y` and `y â‰¤ğ“™ x`
* `Semigroup.HEquiv` - `x ğ“— y` iff `x â‰¤ğ“— y` and `y â‰¤ğ“— x`

The `ğ““` relation is defined as the composition of `ğ“¡` and `ğ“›`:
* `Semigroup.DEquiv` - `x ğ““ y` iff `âˆƒ z, x ğ“¡ z âˆ§ z ğ“› y`

Equivalence classes of elements as sets:
* `Semigroup.REquiv.set` - Notated `âŸ¦xâŸ§ğ“¡`, the `Set S` of elements ğ“¡-related to `x`
* `Semigroup.LEquiv.set` - Notated `âŸ¦xâŸ§ğ“›`, the `Set S` of elements ğ“›-related to `x`
* `Semigroup.JEquiv.set` - Notated `âŸ¦xâŸ§ğ“™`, the `Set S` of elements ğ“™-related to `x`
* `Semigroup.HEquiv.set` - Notated `âŸ¦xâŸ§ğ“—`, the `Set S` of elements ğ“—-related to `x`
* `Semigroup.DEquiv.set` - Notated `âŸ¦xâŸ§ğ““`, the `Set S` of elements ğ““-related to `x`

## Main theorems

* `IsPreorder.SymmClosure` - Given a preorder `Î± : S â†’ S â†’ Prop` and an instance
`[IsPreorder S Î±]`, the symmetric closure of `Î±` is an equivalence relation.

We use the preceding theorem to prove that `ğ“¡, ğ“›, ğ“™, ğ“—` are equivalence relations.
* `Semigroup.REquiv.isEquivalence`
* `Semigroup.LEquiv.isEquivalence`
* `Semigroup.JEquiv.isEquivalence`
* `Semigroup.HEquiv.isEquivalence`

We prove that `â‰¤ğ“¡` and `ğ“¡` are compatible with left multiplication.
That is, if `x (â‰¤)ğ“¡ y`, then `z * x (â‰¤)ğ“¡ z * y`.
We also prove that `â‰¤ğ“›` and `ğ“›` are compatible with right multiplication:
* `Semigroup.RPreorder.lmult_compat`
* `Semigroup.REquiv.lmult_compat`
* `Semigroup.LPreorder.rmult_compat`
* `Semigroup.LEquiv.rmult_compat`

* `Semigroup.rEquiv_lEquiv_comm` - We prove that `ğ“¡` and `ğ“›` commute under composition, i.e.
`(âˆƒ z, x ğ“¡ z âˆ§ z ğ“› y) â†” (âˆƒ z, x ğ“› z âˆ§ z ğ“¡ y)`. This allows us to prove that `ğ““` is symmetric.

We prove that `ğ““` is closed under composition with `ğ“›` and `ğ“¡`.
This allows us to prove that `ğ““` is transitive:
* `Semigroup.DEquiv.closed_under_lEquiv` - If `x ğ““ y` and `y ğ“› z`, then `x ğ““ z`.
* `Semigroup.DEquiv.closed_under_rEquiv` - If `x ğ““ y` and `y ğ“¡ z`, then `x ğ““ z`.

* `Semigroup.DEquiv.isEquivalence` - The `ğ““` relation is reflexive, symmetric, and transitive.

## Implementation notes

Because we defined `ğ“—` as the symmetric closure of `â‰¤ğ“—`, using `simp [HEquiv]` will
change `x ğ“— y` to `x â‰¤ğ“— y âˆ§ y â‰¤ğ“— x`. If you want to rewrite to `x ğ“¡ y âˆ§ x ğ“› y`, use
`HEquiv.iff_rEquiv_and_lEquiv`.

The simp-tagged lemmas at the end of the file are able to close goals like `x * y â‰¤ğ“¡ x` by
calling `simp`. Also, if you have a goal like `x ğ“™ y`, and a local hypothesis `hr : x ğ“¡ y`,
you can close this goal by `simp [hr]` or `simp_all`.

We also define two lemmas, `le` and `ge`, in the namespaces for `ğ“¡, ğ“›, ğ“™, ğ“—`.
These lemmas restrict hypotheses about equivalences to hypotheses about preorders.
For example, if you have the goal `x â‰¤ğ“¡ y` and a hypothesis `h : x ğ“¡ y`, you can
close that goal with `exact h.le`. Similarly, if the goal is `y â‰¤ğ“¡ x`, you can
use `exact h.ge`.

## References

TODO

## Blueprint

* One blueprint DEF entry for Green's Preorders and Equivalence Relations.
Label : greens-relations
Tagged Lean defs :
  - `Semigroup.RPreorder`
  - `Semigroup.LPreorder`
  - `Semigroup.JPreorder`
  - `Semigroup.HPreorder`
  - `Semigroup.RPreorder.isPreorder`
  - `Semigroup.LPreorder.isPreorder`
  - `Semigroup.JPreorder.isPreorder`
  - `Semigroup.HPreorder.isPreorder`
  - `Semigroup.REquiv`
  - `Semigroup.LEquiv`
  - `Semigroup.JEquiv`
  - `Semigroup.HEquiv`
  - `Semigroup.DEquiv`
  - `Semigroup.REquiv.isEquivalence`
  - `Semigroup.LEquiv.isEquivalence`
  - `Semigroup.JEquiv.isEquivalence`
  - `Semigroup.HEquiv.isEquivalence`
  - `Semigroup.DEquiv.isEquivalence`
Content :
  - Explain definitions of the relations
  - introduce notation for preorders, equivalence, and equivalence classes
  - Explain that the preorders are reflexive and transitive (preorders)
  - Explain that the equivalences are reflexive, symmetric, and transitive (equivalence relations)
Dependencies : None

* One Lemma entry for the right/left mul compatibility lemmas.
Label : greens-relations-mul-compat
tagged lean lemmas :
  - `Semigroup.RPreorder.lmult_compat`
  - `Semigroup.REquiv.lmult_compat`
  - `Semigroup.LPreorder.rmult_compat`
  - `Semigroup.LEquiv.rmult_compat`
Content : Prove it for â‰¤ğ“¡, then for ğ“¡, then say a similar arguement holds for â‰¤ğ“› and ğ“›
Dependencies : greens-relations

* One Lemma entry for the commutation of ğ“¡ and ğ“›.
Label : r-l-comm
tagged lean lemmas :
  - `Semigroup.rEquiv_lEquiv_comm`
Content : Prove it and mention that this is used to prove that ğ““ is symmetric.
Dependencies : greens-relations-mul-compat

* One Lemma entry for the closure of ğ““ under ğ“¡ and ğ“›.
label : d-equiv-closed
tagged lean lemmas :
  - `Semigroup.DEquiv.closed_under_lEquiv`
  - `Semigroup.DEquiv.closed_under_rEquiv`
Content : Prove closure under ğ“›, then say a similar argument holds for closure under ğ“¡.
Dependencies : greens-relations

-/

/-! ### Green's Preorders -/

namespace Semigroup

variable {S} [Semigroup S]

/-- `x` is ğ“¡-below `y` if `x = y` or there exists a `z : S` such that `y * z = x` -/
def RPreorder (x y : S) : Prop := âˆƒ z : WithOne S , â†‘y * z = â†‘x

infix:50 " â‰¤ğ“¡ " => RPreorder

namespace RPreorder

@[simp] lemma refl (x : S) : x â‰¤ğ“¡ x := by use 1; simp

@[trans] lemma trans {x y z : S} (hxy : x â‰¤ğ“¡ y) (hyz : y â‰¤ğ“¡ z) : x â‰¤ğ“¡ z := by
  rcases hxy with âŸ¨w, hwâŸ©
  rcases hyz with âŸ¨v, hvâŸ©
  use (v * w)
  rw [â† mul_assoc, hv, hw]

/-- `â‰¤ğ“¡` is a Preorder -/
instance isPreorder : IsPreorder S RPreorder where
  refl := refl
  trans := by apply trans

end RPreorder

/-- `x` is ğ“›-below `y` if `x = y` or there exists a `z : S` such that `z * y = x` -/
def LPreorder (x y : S) : Prop := âˆƒ z : WithOne S, z * â†‘y = â†‘x

infix:50 " â‰¤ğ“› " => LPreorder

namespace LPreorder

@[simp] lemma refl (x : S) : x â‰¤ğ“› x := by use 1; simp

@[trans] lemma trans {x y z : S} (hxy : x â‰¤ğ“› y) (hyz : y â‰¤ğ“› z) : x â‰¤ğ“› z := by
  rcases hxy with âŸ¨u, huâŸ©
  rcases hyz with âŸ¨v, hvâŸ©
  use u * v
  rw [â† hu, â† hv, mul_assoc]

/-- `â‰¤ğ“›` is a Preorder -/
instance isPreorder : IsPreorder S LPreorder where
  refl := refl
  trans := by apply trans

end LPreorder

/-- `x` is ğ“™-below `y` if there exists `w v : WithOne S` such that `w * â†‘y * v = â†‘x` -/
def JPreorder (x y : S) : Prop := âˆƒ w v : WithOne S, w * â†‘y * v = â†‘x

infix:50 " â‰¤ğ“™ " => JPreorder

namespace JPreorder

@[simp] lemma refl (x : S) : x â‰¤ğ“™ x := by use 1, 1; simp

@[trans] lemma trans {x y z : S} (hxy : x â‰¤ğ“™ y) (hyz : y â‰¤ğ“™ z) : x â‰¤ğ“™ z := by
  rcases hxy with âŸ¨uâ‚, vâ‚, huâŸ©
  rcases hyz with âŸ¨uâ‚‚, vâ‚‚, hvâŸ©
  use uâ‚ * uâ‚‚, vâ‚‚ * vâ‚
  rw [â† hu, â† hv]
  simp [mul_assoc]

/-- The ğ“™-preorder is a preorder. -/
instance isPreorder : IsPreorder S JPreorder where
  refl := refl
  trans := by apply trans

end JPreorder

/-- `x` is ğ“—-below `y` if `x â‰¤ğ“¡ y` and `x â‰¤ğ“› y` -/
def HPreorder (x y : S) : Prop := x â‰¤ğ“¡ y âˆ§ x â‰¤ğ“› y

notation:50 f " â‰¤ğ“— " g:50 => HPreorder f g

namespace HPreorder

@[simp] lemma refl (x : S) : x â‰¤ğ“— x := by simp [HPreorder]

@[trans] lemma trans {x y z : S} (hxy : x â‰¤ğ“— y) (hyz : y â‰¤ğ“— z) : x â‰¤ğ“— z := by
  rcases hxy with âŸ¨h1, h2âŸ©
  rcases hyz with âŸ¨h3, h4âŸ©
  constructor
  Â· apply RPreorder.trans h1 h3
  Â· apply LPreorder.trans h2 h4

/-- `â‰¤ğ“—` is a Preorder -/
instance isPreorder : IsPreorder S HPreorder where
  refl := refl
  trans := by apply trans

end HPreorder

/-! ### Green's Equivalences (except ğ““) -/

/-- The symmetric closure of a preorder is an equivalence relation. -/
-- The `_root_` prefix escapes the current `Semigroup` namespace
def _root_.IsPreorder.SymmClosure {Î± : Type*} (p : Î± â†’ Î± â†’ Prop) [h : IsPreorder Î± p] :
    Equivalence (fun x y â†¦ p x y âˆ§ p y x) where
  refl := by simp [h.refl]
  symm := by aesop
  trans {x y z : Î±} (hâ‚ : p x y âˆ§ p y x) (hâ‚‚ : p y z âˆ§ p z y) : p x z âˆ§ p z x := by
    obtain âŸ¨hxy, hyxâŸ© := hâ‚
    obtain âŸ¨hyz, hzyâŸ© := hâ‚‚
    exact âŸ¨h.trans x y z hxy hyz, h.trans z y x hzy hyxâŸ©

/-- Green's ğ“¡ equivalence relation: the symmetric closure of the ğ“¡-preorder. -/
def REquiv (x y : S) : Prop := x â‰¤ğ“¡ y âˆ§ y â‰¤ğ“¡ x

notation :50 x " ğ“¡ " y:50 => REquiv x y

namespace REquiv

/-- The ğ“¡ relation is an equivalence relation. -/
theorem isEquivalence : Equivalence (fun x y : S â†¦ x ğ“¡ y) := by
  have H := IsPreorder.SymmClosure (fun x y : S â†¦ x â‰¤ğ“¡ y)
  simp_all [REquiv]

@[simp] lemma refl (x : S) : x ğ“¡ x :=
  REquiv.isEquivalence.refl x

@[simp, symm] lemma symm {x y : S} (h : x ğ“¡ y) : y ğ“¡ x :=
  REquiv.isEquivalence.symm h

@[trans] lemma trans {x y z : S} (hâ‚ : x ğ“¡ y) (hâ‚‚ : y ğ“¡ z) : x ğ“¡ z :=
  REquiv.isEquivalence.trans hâ‚ hâ‚‚

/-- The set of all elements ğ“¡-related to `x`. -/
@[simp] def set (x : S) : Set (S) := {y | y ğ“¡ x}

notation "âŸ¦" x "âŸ§ğ“¡" => set x

@[simp] lemma set_refl (x : S) : x âˆˆ âŸ¦xâŸ§ğ“¡ := by
  exact REquiv.refl x


end REquiv

/-- Green's ğ“› equivalence relation: the symmetric closure of the ğ“›-preorder. -/
def LEquiv (x y : S) : Prop := x â‰¤ğ“› y âˆ§ y â‰¤ğ“› x

notation :50 x " ğ“› " y:50 => LEquiv x y

namespace LEquiv

/-- The ğ“› relation is an equivalence relation. -/
theorem isEquivalence : Equivalence (fun x y : S â†¦ x ğ“› y) := by
  have H := IsPreorder.SymmClosure (fun x y : S â†¦ x â‰¤ğ“› y)
  simp_all [LEquiv]

@[simp] lemma refl (x : S) : x ğ“› x :=
  isEquivalence.refl x

@[simp, symm] lemma symm {x y : S} (h : x ğ“› y) : y ğ“› x :=
  isEquivalence.symm h

@[trans] lemma trans {x y z : S} (hâ‚ : x ğ“› y) (hâ‚‚ : y ğ“› z) : x ğ“› z :=
  isEquivalence.trans hâ‚ hâ‚‚

/-- The set of all elements ğ“›-related to `x`. -/
@[simp] def set (x : S) : Set (S) := {y | y ğ“› x}

notation "âŸ¦" x "âŸ§ğ“›" => set x

@[simp] lemma set_refl (x : S) : x âˆˆ âŸ¦xâŸ§ğ“› := by
  apply LEquiv.refl

end LEquiv

/-- Green's ğ“™ equivalence relation: the symmetric closure of the ğ“™-preorder. -/
def JEquiv (x y : S) : Prop := x â‰¤ğ“™ y âˆ§ y â‰¤ğ“™ x

notation :50 x " ğ“™ " y:50 => JEquiv x y

namespace JEquiv

/-- The ğ“™ relation is an equivalence relation. -/
theorem isEquivalence : Equivalence (fun x y : S â†¦ x ğ“™ y) := by
  have H := IsPreorder.SymmClosure (fun x y : S â†¦ x â‰¤ğ“™ y)
  simp_all [JEquiv]

@[simp] lemma refl (x : S) : x ğ“™ x :=
  isEquivalence.refl x

@[simp, symm] lemma symm {x y : S} (h : x ğ“™ y) : y ğ“™ x :=
  isEquivalence.symm h

@[trans] lemma trans {x y z : S} (hâ‚ : x ğ“™ y) (hâ‚‚ : y ğ“™ z) : x ğ“™ z :=
  isEquivalence.trans hâ‚ hâ‚‚

/-- The set of all elements ğ“™-related to `x`. -/
@[simp] def set (x : S) : Set (S) := {y | y ğ“™ x}

notation "âŸ¦" x "âŸ§ğ“™" => set x

@[simp] lemma set_refl (x : S) : x âˆˆ âŸ¦xâŸ§ğ“™ := by
  apply JEquiv.refl

end JEquiv

/-- Green's ğ“— equivalence relation: the symmetric closure of the ğ“—-preorder. -/
def HEquiv (x y : S) : Prop := x â‰¤ğ“— y âˆ§ y â‰¤ğ“— x

notation :50 x " ğ“— " y:50 => HEquiv x y

namespace HEquiv

/-- The ğ“— relation is an equivalence relation. -/
theorem isEquivalence : Equivalence (fun x y : S â†¦ x ğ“— y) := by
  have H := IsPreorder.SymmClosure (fun x y : S â†¦ x â‰¤ğ“— y)
  simp_all [HEquiv]

@[simp] lemma refl (x : S) : x ğ“— x :=
  isEquivalence.refl x

@[simp, symm] lemma symm {x y : S} (h : x ğ“— y) : y ğ“— x :=
  isEquivalence.symm h

@[trans] lemma trans {x y z : S} (hâ‚ : x ğ“— y) (hâ‚‚ : y ğ“— z) : x ğ“— z :=
  isEquivalence.trans hâ‚ hâ‚‚

/-- The set of all elements ğ“—-related to `x`. -/
@[simp] def set (x : S) : Set (S) := {y | y ğ“— x}

notation "âŸ¦" x "âŸ§ğ“—" => set x

@[simp] lemma set_refl (x : S) : x âˆˆ âŸ¦xâŸ§ğ“— := by
  apply HEquiv.refl

/-- The ğ“— equivalence relation is the intersection of ğ“¡ and ğ“› equivalences. -/
theorem iff_rEquiv_and_lEquiv (x y : S) : x ğ“— y â†” x ğ“¡ y âˆ§ x ğ“› y := by
  simp_all [HEquiv, HPreorder, LEquiv, REquiv]
  tauto

end HEquiv

/-! ### ğ“¡ ğ“› Theorems -/

/-- The ğ“¡-preorder is compatible with left multiplication. -/
@[simp] lemma RPreorder.lmult_compat {x y : S} (h : x â‰¤ğ“¡ y) (z : S) : z * x â‰¤ğ“¡ z * y := by
  obtain âŸ¨u, huâŸ© := h; use u
  simp [mul_assoc, hu]

/-- The ğ“¡ equivalence is compatible with left multiplication. -/
@[simp] lemma REquiv.lmult_compat {x y : S} (h : x ğ“¡ y) (z : S) : z * x ğ“¡ z * y := by
  simp_all [REquiv]

/-- The ğ“›-preorder is compatible with right multiplication. -/
@[simp] lemma LPreorder.rmult_compat {x y : S} (h : x â‰¤ğ“› y) (z : S) : x * z â‰¤ğ“› y * z := by
  rcases h with âŸ¨u, huâŸ©
  use u
  simp [â† mul_assoc, hu]

/-- The ğ“› equivalence is compatible with right multiplication. -/
@[simp] lemma LEquiv.rmult_compat {x y : S} (h : x ğ“› y) (z : S) : x * z ğ“› y * z := by
  simp_all [LEquiv]

/-- The ğ“¡ and ğ“› relations commute under composition. -/
theorem rEquiv_lEquiv_comm (x y : S) : (âˆƒ z, x ğ“¡ z âˆ§ z ğ“› y) â†” (âˆƒ z, x ğ“› z âˆ§ z ğ“¡ y) := by
  constructor
  Â· rintro âŸ¨z, âŸ¨hr, hlâŸ©âŸ©
    have hrâ‚ := hr
    have hlâ‚ := hl
    rcases hrâ‚ with âŸ¨âŸ¨wâ‚, hwâ‚âŸ©, âŸ¨vâ‚, hvâ‚âŸ©âŸ©
    rcases hlâ‚ with âŸ¨âŸ¨wâ‚‚, hwâ‚‚âŸ©, âŸ¨vâ‚‚, hvâ‚‚âŸ©âŸ©
    -- We have `x = wâ‚‚ * y * wâ‚` and `y = vâ‚‚ * z * vâ‚`
    -- We want to use `vâ‚‚ * z * wâ‚`, but we need to destruct on `vâ‚‚, wâ‚ : WithOne S`
    cases hvâ‚‚one : vâ‚‚ with
    | one =>
      subst vâ‚‚ -- trivial case where `vâ‚‚ = 1`
      simp at hvâ‚‚
      subst z -- `z = y`
      use x; simp [hr]
    | coe vâ‚‚ =>
      subst hvâ‚‚one
      cases hwâ‚one : wâ‚ with
      | one =>
        subst wâ‚ -- trivial case where `wâ‚ = 1` and `z = x`
        simp at hwâ‚
        subst hwâ‚
        use y; simp [hl]
      | coe wâ‚ => -- Non-trivial case, use `vâ‚‚ * z * wâ‚`
        subst hwâ‚one
        use vâ‚‚ * z * wâ‚
        constructor
          <;> constructor
        Â· use wâ‚‚ -- `x â‰¤ğ“› vâ‚‚ * z * wâ‚`
          simp [â† mul_assoc]
          rw [mul_assoc wâ‚‚, hvâ‚‚, hwâ‚‚, â† hwâ‚]
        Â· use vâ‚‚ -- `vâ‚‚ * z * wâ‚ â‰¤ğ“› x`
          simp [mul_assoc]
          rw [â† hwâ‚]
        Â· use wâ‚ -- `vâ‚‚ * z * wâ‚ â‰¤ğ“¡ y`
          simp [hvâ‚‚]
        Â· use vâ‚ -- `y â‰¤ğ“¡ vâ‚‚ * z * wâ‚`
          simp [mul_assoc vâ‚‚]
          rw [hwâ‚, â† hvâ‚‚, â† hvâ‚]
          simp [mul_assoc]
  Â· rintro âŸ¨z, âŸ¨hl, hrâŸ©âŸ©
    have hrâ‚ := hr
    have hlâ‚ := hl
    rcases hrâ‚ with âŸ¨âŸ¨wâ‚, hwâ‚âŸ©, âŸ¨vâ‚, hvâ‚âŸ©âŸ©
    rcases hlâ‚ with âŸ¨âŸ¨wâ‚‚, hwâ‚‚âŸ©, âŸ¨vâ‚‚, hvâ‚‚âŸ©âŸ©
    -- We have `y = vâ‚‚ * x * vâ‚` and `x = wâ‚‚ * y * wâ‚`
    -- We want to use `wâ‚‚ * z * vâ‚`, but we need to destruct on `wâ‚‚, vâ‚ : WithOne S`
    cases hwâ‚‚one : wâ‚‚ with
    | one =>
      subst wâ‚‚ -- trivial case where `wâ‚‚ = 1`
      simp at hwâ‚‚
      subst z -- `z = x`
      use y; simp [hr]
    | coe wâ‚‚ =>
      subst hwâ‚‚one
      cases hvâ‚one : vâ‚ with
      | one =>
        subst vâ‚ -- trivial case where `vâ‚ = 1` and `z = y`
        simp at hvâ‚
        subst hvâ‚
        use x; simp [hl]
      | coe vâ‚ => -- Non-trivial case, use `wâ‚‚ * z * vâ‚`
        subst hvâ‚one
        use wâ‚‚ * z * vâ‚
        constructor
          <;> constructor
        Â· use wâ‚; simp -- `x â‰¤ğ“¡ wâ‚‚ * z * vâ‚`
          nth_rw 1 [â† hwâ‚‚]
          conv => lhs; lhs; rw [mul_assoc, hvâ‚]
          rw [mul_assoc, hwâ‚]
        Â· use vâ‚ -- `wâ‚‚ * z * vâ‚ â‰¤ğ“¡ x`
          simp [hwâ‚‚]
        Â· use wâ‚‚ -- `wâ‚‚ * z * vâ‚ â‰¤ğ“› y`
          simp [â† hvâ‚, â† mul_assoc]
        Â· use vâ‚‚ -- `y â‰¤ğ“› wâ‚‚ * z * vâ‚`
          simp [hwâ‚‚, â† mul_assoc, hvâ‚‚, hvâ‚]

/-! ### Green's D relation -/

/-- Green's ğ““ equivalence relation: defined as the composition of ğ“¡ and ğ“›. -/
def DEquiv : S â†’ S â†’ Prop := fun x y => âˆƒ z, x ğ“¡ z âˆ§ z ğ“› y

infix:50 " ğ““ " => DEquiv

namespace DEquiv

@[simp] lemma refl (x : S) : x ğ““ x := by
  use x
  constructor <;> simp

@[simp, symm] lemma symm {x y : S} (h : x ğ““ y) : y ğ““ x := by
  obtain âŸ¨z, âŸ¨hz1, hz2âŸ©âŸ© := h
  simp [DEquiv]
  rw [rEquiv_lEquiv_comm]
  use z
  simp_all

/-- The ğ““ relation is closed under composition with ğ“›. -/
lemma closed_under_lEquiv {x y z : S} (hd : x ğ““ y) (hl : y ğ“› z) : x ğ““ z := by
  rcases hd with âŸ¨w, âŸ¨hw1, hw2âŸ©âŸ©
  use w
  simp_all
  apply LEquiv.trans hw2 hl

/-- The ğ““ relation is closed under composition with ğ“¡. -/
lemma closed_under_rEquiv {x y z : S} (hd : x ğ““ y) (hl : y ğ“¡ z) : x ğ““ z := by
  simp [DEquiv] at hd
  rw [rEquiv_lEquiv_comm] at hd
  rcases hd with âŸ¨w, âŸ¨hw1, hw2âŸ©âŸ©
  symm
  use w
  simp_all
  apply REquiv.trans hl.symm hw2.symm

@[trans] lemma trans {x y z : S} (h1 : x ğ““ y) (h2 : y ğ““ z) : x ğ““ z := by
  rcases h2 with âŸ¨w, âŸ¨hw1, hw2âŸ©âŸ©
  have hd1 : x ğ““ w := by apply closed_under_rEquiv <;> assumption
  apply closed_under_lEquiv hd1 hw2

/-- The ğ““ relation is an equivalence relation. -/
theorem isEquivalence : Equivalence (fun x y : S => x ğ““ y) where
  refl := refl
  symm := symm
  trans := trans

@[simp] def set (x : S) : Set (S) := {y | y ğ““ x}

notation "âŸ¦" x "âŸ§ğ““" => set x

@[simp] lemma set_refl (x : S) : x âˆˆ âŸ¦xâŸ§ğ““ := by
  apply DEquiv.refl

end DEquiv

/-! ### Simp Lemmas -/

variable {x y z : S}

/-- `x â‰¤ğ“¡ y` implies `x â‰¤ğ“™ y` -/
@[simp] lemma RPreorder.to_jEquiv (h : x â‰¤ğ“¡ y) : x â‰¤ğ“™ y := by
  obtain âŸ¨u, huâŸ© := h
  use 1, u
  simp_all

/-- `x â‰¤ğ“› y` implies `x â‰¤ğ“™ y` -/
@[simp] lemma LPreorder.to_jEquiv (h : x â‰¤ğ“› y) : x â‰¤ğ“™ y := by
  obtain âŸ¨u, huâŸ© := h
  use u, 1
  simp_all

/-- `x ğ“¡ y` implies `x ğ“™ y` -/
@[simp] lemma REquiv.to_jEquiv (h : x ğ“¡ y) : x ğ“™ y := by
  simp_all [JEquiv, REquiv]

/-- `x ğ“› y` implies `x ğ“™ y` -/
@[simp] lemma LEquiv.to_jEquiv (h : x ğ“› y) : x ğ“™ y := by
  simp_all [JEquiv, LEquiv]

/-- `x ğ““ y` implies `x ğ“™ y` -/
@[simp] lemma DEquiv.to_jEquiv (h : x ğ““ y) : x ğ“™ y := by
  rcases h with âŸ¨z, âŸ¨hr, hlâŸ©âŸ©
  have hrâ‚ := hr
  have hlâ‚ := hl
  rcases hrâ‚ with âŸ¨âŸ¨o, hoâŸ©, âŸ¨u, huâŸ©âŸ©
  rcases hlâ‚ with âŸ¨âŸ¨v, hvâŸ©, âŸ¨w, hwâŸ©âŸ©
  constructor
  Â· use v, o
    rw [hv, ho]
  Â· use w, u
    rw [â† hw, â† hu]
    simp [â† mul_assoc]

/-- `x ğ“— y` implies `x ğ“¡ y` -/
@[simp] lemma HEquiv.to_rEquiv (h : x ğ“— y) : x ğ“¡ y := by
  rw [HEquiv.iff_rEquiv_and_lEquiv] at h
  simp [h]

/-- `x ğ“— y` implies `x ğ“› y` -/
@[simp] lemma HEquiv.to_lEquiv (h : x ğ“— y) : x ğ“› y := by
  rw [HEquiv.iff_rEquiv_and_lEquiv] at h
  simp [h]

/-- `x * y` is always ğ“¡-below `x` -/
@[simp] lemma RPreorder.mul_right_self : x * y â‰¤ğ“¡ x := by
  use y; rw [WithOne.coe_mul]

/-- `x * y` is always ğ“›-below `y` -/
@[simp] lemma LPreorder.mul_left_self : x * y â‰¤ğ“› y := by
  use x; rw [WithOne.coe_mul]

/-- `x * y * z` is always ğ“™-below `y` -/
@[simp] lemma JPreorder.mul_sandwich_self : x * y * z â‰¤ğ“™ y := by
  use x, z; simp

/-- `x â‰¤ğ“¡ x * y` implies `x ğ“¡ x * y` -/
@[simp] lemma REquiv.of_preorder_mul_right (h : x â‰¤ğ“¡ x * y) : x ğ“¡ x * y := by
  simp_all [REquiv]

/-- `x â‰¤ğ“› y * x` implies `x ğ“› y * x` -/
@[simp] lemma LEquiv.of_preorder_mul_left (h : x â‰¤ğ“› y * x) : x ğ“› y * x := by
  simp_all [LEquiv]

/-- `x â‰¤ğ“™ y * x * z` implies `x ğ“™ y * x * z` -/
@[simp] lemma JEquiv.of_preorder_mul_sandwich (h : x â‰¤ğ“™ y * x * z) :
    x ğ“™ y * x * z := by simp_all [JEquiv]

/-- If `x ğ“¡ x * y * z`, then `x ğ“¡ x * y`. -/
@[simp] lemma REquiv.right_cancel (h : x ğ“¡ x * y * z) : x ğ“¡ x * y := by
  simp_all [REquiv]
  obtain âŸ¨âŸ¨u, huâŸ©, _âŸ© := h
  use z * u
  simp_rw [WithOne.coe_mul, â† mul_assoc] at *
  exact hu

/-- If `x ğ“¡ x * y * z`, then `x * y ğ“¡ x * y * z`. -/
@[simp] lemma REquiv.right_extend (h : x ğ“¡ x * y * z) : x * y ğ“¡ x * y * z := by
  simp_all [REquiv]
  obtain âŸ¨âŸ¨u, huâŸ©, _âŸ© := h
  use u * y
  simp_rw [WithOne.coe_mul, â† mul_assoc] at *
  rw [hu]

/-- If `x ğ“› z * y * x`, then `x ğ“› y * x`. -/
@[simp] lemma LEquiv.left_cancel (h : x ğ“› z * y * x) : x ğ“› y * x := by
  simp_all [LEquiv]
  obtain âŸ¨u, huâŸ© := h
  use u * z
  simp_rw [WithOne.coe_mul, â† mul_assoc] at *
  exact hu

/-- If `x ğ“› z * y * x`, then `y * x ğ“› z * y * x`. -/
@[simp] lemma LEquiv.left_extend (h : x ğ“› z * y * x) : y * x ğ“› z * y * x := by
  simp_all [LEquiv]
  obtain âŸ¨u, huâŸ© := h
  use y * u
  simp_rw [WithOne.coe_mul, â† mul_assoc] at *
  have hrw : â†‘y * u * â†‘z * â†‘y * â†‘x  = â†‘y * (u * â†‘z * â†‘y * â†‘x) := by simp [â† mul_assoc]
  rw [hrw, hu]

/-! #### `le` and `ge` lemmas -/

@[simp] lemma REquiv.le (h : x ğ“¡ y) : x â‰¤ğ“¡ y := by simp_all [REquiv]

@[simp] lemma REquiv.ge (h : x ğ“¡ y) : y â‰¤ğ“¡ x := by simp_all [REquiv]

@[simp] lemma LEquiv.le (h : x ğ“› y) : x â‰¤ğ“› y := by simp_all [LEquiv]

@[simp] lemma LEquiv.ge (h : x ğ“› y) : y â‰¤ğ“› x := by simp_all [LEquiv]

@[simp] lemma JEquiv.le (h : x ğ“™ y) : x â‰¤ğ“™ y := by simp_all [JEquiv]

@[simp] lemma JEquiv.ge (h : x ğ“™ y) : y â‰¤ğ“™ x := by simp_all [JEquiv]

@[simp] lemma HEquiv.le (h : x ğ“— y) : x â‰¤ğ“— y := by simp_all [HEquiv]

@[simp] lemma HEquiv.ge (h : x ğ“— y) : y â‰¤ğ“— x := by simp_all [HEquiv]


end Semigroup
